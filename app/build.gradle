apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "me.iacn.mbestyle"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 21
        versionName "1.6.3"
        resConfigs "en", "zh-rCN"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def file = project.rootProject.file('config.prop')
    def leancloudId = '"+1S"'
    def leancloudKey = '"+1S"'

    if (file.exists()) {
        Properties prop = new Properties()
        InputStream stream = file.newDataInputStream()
        prop.load(stream)
        stream.close()

        leancloudId = prop.getProperty('leancloudId')
        leancloudKey = prop.getProperty('leancloudKey')
    }

    buildTypes.each {
        it.buildConfigField 'String', 'LEANCLOUD_ID', leancloudId
        it.buildConfigField 'String', 'LEANCLOUD_KEY', leancloudKey
    }

    // 修改生成 Apk 的文件名
    android.applicationVariants.all { variant ->
        def outFile = variant.outputs[0].outputFile
        def outFileName = 'MBEStyle_' + defaultConfig.versionName + '.apk'
        variant.outputs[0].outputFile = new File(outFile.parent, outFileName)
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v13:26.0.0-alpha1'
    compile 'com.android.support:cardview-v7:26.0.0-alpha1'

    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    compile 'com.github.bumptech.glide:glide:4.0.0'
    compile 'com.wang.avi:library:2.1.3'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'moe.feng:AlipayZeroSdk:1.1'
}