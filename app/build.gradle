apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"

    defaultConfig {
        applicationId "me.iacn.mbestyle"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 24
        versionName "1.7.0"
        resConfigs "en", "zh-rCN"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def file = project.rootProject.file('config.prop')
    def leancloudId = '"+1S"'
    def leancloudKey = '"+1S"'

    if (file.exists()) {
        Properties prop = new Properties()
        InputStream stream = file.newDataInputStream()
        prop.load(stream)
        stream.close()

        leancloudId = prop.getProperty('leancloudId')
        leancloudKey = prop.getProperty('leancloudKey')
    }

    buildTypes.each {
        it.buildConfigField 'String', 'LEANCLOUD_ID', leancloudId
        it.buildConfigField 'String', 'LEANCLOUD_KEY', leancloudKey
    }

    // 修改生成 Apk 的文件名
    /*android.applicationVariants.all { variant ->
        def outFile = variant.outputs[0].outputFile
        def outFileName = 'MBEStyle_' + defaultConfig.versionName + '.apk'
        variant.outputs[0].outputFile = new File(outFile.parent, outFileName)
    }*/
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:design:27.0.1'
    implementation 'com.android.support:support-v13:27.0.1'
    implementation 'com.android.support:cardview-v7:27.0.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.wang.avi:library:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'moe.feng:AlipayZeroSdk:1.1'
}